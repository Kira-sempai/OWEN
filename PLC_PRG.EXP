
(* @NESTEDCOMMENTS := 'Yes' *)
(* @PATH := '' *)
(* @OBJECTFLAGS := '0, 8' *)
(* @SYMFILEFLAGS := '2048' *)
PROGRAM PLC_PRG
VAR CONSTANT
	K14AutoModeDelay : TIME := T#10s;
END_VAR

VAR
	SignalizationResetTrigger : R_TRIG;
	K14PumpsControl : fbK14PumpsControl;
	K15PumpsControl : fbK15PumpsControl;
	K16PumpsControl : fbK16PumpsControl;
	K14ManualModeTOF : TOF;
END_VAR

VAR_TEMP
	i: USINT;
	K14WorkAllowed : BOOL := FALSE;

	K15WorkAllowed: BOOL := FALSE;

	K16WorkAllowed : BOOL := FALSE;
END_VAR
(* @END_DECLARATION := '0' *)

(*
	Сброс всех аварий насосов по замыканию кнопки
	съема аварий
*)
SignalizationResetTrigger(CLK := MV110DI.SignalizationButton);
resetPumpsAlarm(SignalizationResetTrigger.Q);


(*
	Включение сигнала на UZ
	по тому же сигналу, что и MV110DI.TurnOnK15AndK16
*)
FOR i := 1 TO MAX_K14_PUMPS_NUM DO
	PLC160DO.UZStart[i] := MV110DI.TurnOnK15AndK16;
END_FOR;

(*
	3. Управление насосами К14
	контура отопления и вентиляции
*)
FOR i := 1 TO MAX_K14_PUMPS_NUM DO
	MU110DO.K14PumpIsOn[i] := PLC160DI.UZStatus[i].Running;
	(*Включить/Выключить сигнальную лампу состояния насоса*)

	(*если хоть один из насосов исправен, то работа разрешена*)
	IF NOT(K14PumpFaulty[i]) THEN
		K14WorkAllowed := TRUE;
	END_IF;
END_FOR;

K14ManualModeTOF(IN := NOT(PLC160DI.K14_PumpsModeIsAuto), PT := K14AutoModeDelay);

K14PumpsControl(
	ManualMode						:= K14ManualModeTOF.Q,
	WorkAllowed						:= K14WorkAllowed,
	SummerMode						:= NOT(PLC160DI.K14_PumpsModeIsHeating),
	EmergencyPumpStopButtonPressed := PLC160DI.K14_PumpStop,
	PumpState						:= MU110DO.K14PumpIsOn,
	PressureDrop					:= NOT(PLC160DI.K14_NoPressureDrop)
);

(*
	6. Управление насосами К15
	греющего контура ГВС
*)
FOR i := 1 TO MAX_K15_PUMPS_NUM DO
	(*если хоть один из насосов исправен, то работа разрешена*)
	IF NOT(K15PumpFaulty[i]) THEN
		K15WorkAllowed := TRUE;
	END_IF;
END_FOR;

K15PumpsControl(
	ManualMode						:= NOT(MV110DI.HeatingDHWPumpGroupModeIsAuto),
	WorkAllowed						:= K15WorkAllowed,
	EmergencyPumpStopButtonPressed := MV110DI.K15_StopButton,
	PumpState						:= PLC160DO.K15Pump,
	PressureDrop					:= NOT(MV110DI.K15_NoPressureDrop)
);

(*
	5. Управление насосами К16
	контура ГВС
*)
FOR i := 1 TO MAX_K16_PUMPS_NUM DO
	(*если хоть один из насосов исправен, то работа разрешена*)
	IF NOT(K16PumpFaulty[i]) THEN
		K16WorkAllowed := TRUE;
	END_IF;
END_FOR;

K16PumpsControl(
	ManualMode						:= NOT(MV110DI.DHWPumpGroupModeIsAuto),
	WorkAllowed						:= K16WorkAllowed,
	EmergencyPumpStopButtonPressed := MV110DI.K16_StopButton,
	PumpState						:= PLC160DO.K16Pump,
	PressureDrop					:= NOT(MV110DI.K16_NoPressureDrop)
);
END_PROGRAM
