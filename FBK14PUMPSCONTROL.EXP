
(* @NESTEDCOMMENTS := 'Yes' *)
(* @PATH := '\/K14' *)
(* @OBJECTFLAGS := '0, 8' *)
(* @SYMFILEFLAGS := '2048' *)
FUNCTION_BLOCK fbK14PumpsControl
(*
	3. Управление насосами К14
	контура отопления и вентиляции
*)
VAR CONSTANT
	PumpCheckDelayTime : TIME := T#30s;
END_VAR

VAR_INPUT
	ManualMode: BOOL;						(*Ручной режим?*)
	WorkAllowed: BOOL;					(*Работа разрешена?*)
	SummerMode: BOOL;						(*Летний режим?*)
	EmergencyPumpStopButtonPressed: ARRAY [1..MAX_K14_PUMPS_NUM] OF BOOL; (*Нажат аварийный стоп насоса?*)
	PumpState: ARRAY [1..MAX_K14_PUMPS_NUM] OF BOOL;	(*Работает насос?*)
	PressureDrop : BOOL;					(*Есть разница давлений?*)
END_VAR
VAR
	pumpStatusCheckDelay	: ARRAY [1..MAX_K14_PUMPS_NUM] OF TON;
	workOrderControl : fbK14WorkOrderControl;
	pumpsTrainingControl : fbK14PumpsTraining;
END_VAR
VAR_TEMP
	i: USINT;
	pumpWorkOrder : ARRAY [1..MAX_K14_PUMPS_NUM] OF USINT;
	pumpStatusCheck : ARRAY [1..MAX_K14_PUMPS_NUM] OF BOOL;
	break : BOOL := FALSE;
END_VAR
(* @END_DECLARATION := '0' *)
break := FALSE;
(*Проверяем исправность насосов К14*)
FOR i := 1 TO MAX_K14_PUMPS_NUM DO
	pumpStatusCheckDelay[i](IN := PumpState[i], PT := PumpCheckDelayTime);
	pumpStatusCheck[i] := pumpStatusCheckDelay[i].Q;
END_FOR;

fK14PumpsFaultyControl(
	PumpState				:= PumpState,
	PumpStatusCheckDelay	:= pumpStatusCheck,
	PressureDrop			:= PressureDrop
);

(*Управление насосами К14*)
IF ManualMode THEN (*Ручной режим?*)
(*
	FOR i := 1 TO MAX_K14_PUMPS_NUM DO
		fK14PumpManualControl(
			PumpID	:= i,
			TurnOn	:= TurnOnPumpButtonPressed[i],
			TurnOff	:= TurnOffPumpButtonPressed[i],
			State		:= PumpState[i],
			EmergencyStop := EmergencyPumpStopButtonPressed[i]
		);
	END_FOR;
*)
	(*Контроллер никак не участвует в ручном режиме*)
	FOR i := 1 TO MAX_K14_PUMPS_NUM DO
		fTurnOffK14Pump(i);
	END_FOR;
ELSE
	IF WorkAllowed THEN	(*Работа разрешена?*)
		(*определяем порядок включения насосов*)
		workOrderControl( SummerMode := SummerMode,
								pumpWorkOrder := pumpWorkOrder);

		FOR i := 1 TO MAX_K14_PUMPS_NUM DO
			(*
				работаем с насосами в порядке их приоритета
				Ищем первый исправный насос и включаем его
			*)
			IF NOT(break) AND NOT(K14PumpFaulty[pumpWorkOrder[i]]) THEN
				fK14PumpAutoControl(
						PumpID := pumpWorkOrder[i],
						EmergencyPumpStopButtonPressed := EmergencyPumpStopButtonPressed[pumpWorkOrder[i]]
				);
				break := TRUE;
			END_IF;
		END_FOR;
		(*периодическая тренировка насосов*)
		pumpsTrainingControl();
	ELSE
		(*
			если работа не разрешена,
			выключаем все насосы
		*)
		FOR i := 1 TO MAX_K14_PUMPS_NUM DO
			fTurnOffK14Pump(i);
		END_FOR;
		RETURN;
	END_IF;
END_IF;
END_FUNCTION_BLOCK
